#!/usr/bin/env fish

set laptops forge
set power_adapter_forge /sys/class/power_supply/macsmc-ac/online

set servers snowpi

cd ~/nixos

# rebuilds the system.
# this is more complex than usually necessary because the gpu driver for asahi
# is experimental, and i do not want to rebuild the world
function rebuild
    set cmd doas nixos-rebuild switch
    set host $hostname
    if test (count $argv) -ge 1
        set host $argv[1]
        set -a cmd --target-host $host
    end
    set -a cmd --flake .#$host
    test (uname -r | cut -f2 -d'-') = asahi; and set -a cmd --impure
    eval $cmd
    set -e __snow_curr_gen_sec
end

function get_curr_gen
    nixos-rebuild list-generations --json | jq -r ".[] | select(.current) | .$argv[1]"
end

switch $argv[1]
    case check
        set -qU __snow_curr_gen_sec
        or set -U __snow_curr_gen_sec (date --date=(get_curr_gen date) +'%s')
        set mod_times (stat ./**/*.* -c '%Y %n')
        set newer_files
        for tn in $mod_times
            set time (echo $tn | cut -f1 -d' ')
            test $time -gt $__snow_curr_gen_sec
            and set -a newer_files (echo $tn | cut -f2 -d' ')
        end
        echo $newer_files
        test (count $newer_files) -ne 0; and exit 1
    case update
        test (contains $hostname $laptops)
        and test (cat $power_adapter_$hostname) = 0
        and read -P 'warn: on battery power. continue? ' response

        test $response -eq y; or exit

        nix flake update
        test $status -eq 0; and rebuild
    case deploy
        if test (count $argv) -eq 1
            echo 'must pass at least one host to build and deploy to'
            exit
        end
        for host in $argv[2..]
            contains $host $servers
            and rebuild $host
            or echo "$host is not a server."
        end
    case rebuild
        rebuild
    case edit
        hx .
        test $status -ne 0; and exit
        git add --all
        rebuild
        test $status -ne 0; and exit
        set current (get_curr_gen generation)
        git commit -m "generation $hostname-$current"
        git push
    case '*'
        echo 'options: rebuild check edit update deploy'
end

exit 0
